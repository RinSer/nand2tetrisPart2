/**
 * Implements the Zombie game.
 * 
 */

class ZombieGame {
   field Player player;
   field Bullet bullet;
   field int direction;
   field int bulletDirection;
   field boolean isShooting;

   /** Constructs a new Zombie Game. */
   constructor ZombieGame new() {
      let isShooting = false;
      let player = Player.new(0, 0);
      do player.erase();
      do player.draw();
      return this;
   }

   /** Disposes this game. */
   method void dispose() {
      do player.dispose();
      do Memory.deAlloc(this);
      return;
   }

   /** Moves the player in the current direction. */
   method void movePlayer() {
      if (direction = 1) { do player.moveUp(); do Sys.wait(5); }
      if (direction = 2) { do player.moveDown(); do Sys.wait(5); }
      if (direction = 3) { do player.moveLeft(); do Sys.wait(100); }
      if (direction = 4) { do player.moveRight(); do Sys.wait(100); }
      return;
   }

   method void shoot() {
      var int location;
      let location = player.getLocation();
      if (isShooting) {
         if (~(direction = bullet.getDirection())) {
            let bullet = Bullet.new(location, direction, bullet);
         }
      } else {
         let bullet = Bullet.new(location, direction, null);
         let isShooting = true;
      }
      return;
   }

   method void processShooting() {
      var boolean stop;
      var Bullet current;
      var Bullet dead;
      // shooting
      if (isShooting) {
         let current = bullet;
         if (current = null) {
            let isShooting = false;
         } else {
            if (current.isOver()) {
               let bullet = current.getPrevious();
               do current.dispose();
            } else {
               while (~(current = null)) {
                  let stop = current.isOver();
                  if (stop) {
                     let dead = current;
                     let current = current.getPrevious();
                  } else {
                     do current.moveTo();
                     do Sys.wait(5);
                     let current = current.getPrevious();
                  }
               }    
            }
         }
      }
      return;
   }

   /** Runs the game: handles the user's inputs and moves the zombie accordingly */
   method void run() {
      var char key;  // the key currently pressed by the user
      var boolean exit;
      let exit = false;
      
      while (~exit) {
         // waits for a key to be pressed
         while (key = 0) {
            let key = Keyboard.keyPressed();
            do player.draw();
            do processShooting();
         }
         if (key = 81)  { let exit = true; }     // q key
         if (key = 131) { let direction = 1; }   // up arrow
         if (key = 133) { let direction = 2; }   // down arrow
         if (key = 130) { let direction = 3; }   // left arrow
         if (key = 132) { let direction = 4; }   // right arrow
         if (key = 32) { do shoot(); } // space

         // waits for the key to be released
         while (~(key = 0)) {
            let key = Keyboard.keyPressed();
            if ((key > 129) & (key < 134)) {
               do movePlayer();
            }
            do processShooting();
         }
     } // while
     return;
   }
}



